import party.davidsherenowitsa.transparensbee.genutils.LogListGenerator

apply plugin: 'com.android.application'

def generatedSrcDir = new File(buildDir, "generated/logs/src/main/java")
def resourceDir = new File(projectDir, "../buildSrc/src/main/resources/party/davidsherenowitsa/transparensbee/genutils")

task generateLogListClass {
    doLast {
        LogListGenerator.generateLogListClass(resourceDir, generatedSrcDir)
    }
}

preBuild.dependsOn generateLogListClass

task downloadLogList {
    doLast
    {
        LogListGenerator.downloadLogList(resourceDir)
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "party.davidsherenowitsa.transparensbee"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 7
        versionName "1.6"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java {
                srcDirs += generatedSrcDir
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
}
